# cat ../inputs/little_women_part_1.txt \
#     | tr -cs '[:alpha:]' '\n' \
#     > 1.split_words.txt
# sort 1.split_words.txt | uniq -c \
#     > 2.count_words.txt
# sort -k1,1nr 2.count_words.txt \
#     | awk 'BEGIN {print "word\tcount"} { print $2 "\t" $1}' \
#     > 3.sort_counts.txt
# egrep '^(Jo|Amy|Laurie|Beth)\t' 3.sort_counts.txt > 4.select_words.txt
configfile: 'config.yaml'

rule all:
    input: expand("outputs/{x}.select_words.txt", \
              x=config['input_base_filenames'])



rule split_words:
    input: "inputs/{base_name}.txt"
    output: "outputs/{base_name}.split_words.txt"
    shell: "cat {input} | tr -cs '[:alpha:]' '\n' > {output}"

rule count_words:
    input: "outputs/{base_name}.split_words.txt"
    output: "outputs/{base_name}.count_words.txt"
    shell: "sort {input} | uniq -c > {output}"

rule sort_counts:
    input: "outputs/{base_name}.count_words.txt"
    output: "outputs/{base_name}.sort_counts.txt"
    shell: '''
        sort -k1,1nr {input} \
            | awk 'BEGIN {{print "word\tcount"}} {{ print $2 "\t" $1}}' \
            > {output}
        '''

rule select_words:
    input: "outputs/{base_name}.sort_counts.txt"
    output: "outputs/{base_name}.select_words.txt"
    params: regex=config['select_words_regex']
    shell: "egrep '{params.regex}' {input} > {output}"
